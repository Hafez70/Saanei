apply plugin: 'com.android.library'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'folioreader'

    publishedGroupId = 'com.folioreader'
    libraryName = 'FolioReader'
    artifact = 'folioreader'

    libraryDescription = 'An epub reader for Android'

    siteUrl = 'https://github.com/FolioReader/FolioReader-Android'
    gitUrl = 'https://github.com/FolioReader/FolioReader-Android.git'

    libraryVersion = '0.4.4'

    developerId = 'mobisystech'
    developerName = 'Folio Reader'
    developerEmail = 'mahavir@codetoart.com'

    licenseName = 'FreeBSD License'
    licenseUrl = 'https://en.wikipedia.org/wiki/FreeBSD_Documentation_License#License'
    allLicenses = ["FreeBSD"]
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['res']
        }
        test {
            java.srcDirs = ['src/test/java']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        abortOnError false
    }

    checkstyle {
        ignoreFailures = true
    }
}

repositories {
    maven {
        url "http://dl.bintray.com/mobisystech/maven"
    }
}

apply from: '../folioreader/bintray/installv1.gradle'
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':webViewMarker')

    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:recyclerview-v7:28.0.0"
    implementation "com.android.support:support-v4:28.0.0"
    implementation "com.android.support:design:28.0.0"

    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.2.61"

    // r2-streamer
    api "org.readium:r2-fetcher:0.1.8"
    api "org.readium:r2-parser:0.1.8"
    api "org.readium:r2-server:0.1.8"

    implementation 'org.greenrobot:eventbus:3.1.1'
}
apply from: '../folioreader/bintray/bintrayv1.gradle'

task deleteJar(type: Delete) {
    delete 'libs/jars/my_folio.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('libs/jars/')
    include('classes.jar')
    rename('classes.jar', 'my_folio.jar')
}

createJar.dependsOn(deleteJar, build)